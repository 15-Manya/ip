plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'

    // JavaFX - include ALL platforms for cross-platform JAR
    def javafxVersion = '21.0.2'
    def platforms = ['mac', 'win', 'linux']
    def modules = ['javafx-base', 'javafx-controls', 'javafx-fxml', 'javafx-graphics']
    
    platforms.each { platform ->
        modules.each { module ->
            implementation "org.openjfx:${module}:${javafxVersion}:${platform}"
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClass.set("agentsmith.Launcher")
}

shadowJar {
    archiveBaseName = "agentsmith"
    archiveClassifier = null
}

run {
    standardInput = System.in
    jvmArgs += [
            '--module-path', configurations.runtimeClasspath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml'
    ]
}

checkstyle {
    toolVersion = '11.0.0'
    configFile = file('config/checkstyle/checkstyle.xml')
}
